#version 450
precision highp float;

#include "common.glsl"
#include "ray.glsl"
#include "types.glsl"
#include "buffers.glsl"
#include "camera.glsl"
#include "intersection.glsl"

// -------------
// Ray Color
// -------------
vec3 ray_color(Ray r, inout uvec3 seed) {
    int depth = 0;
    int max_depth = 10;

    HitRec rec;
    bool hitLight = false;

    vec3 res = vec3(0.0);
    vec3 throughput = vec3(1.0);

    while (depth < max_depth && hit_world(r, 0.001, FLT_MAX, rec) && !hitLight) {
        seed = seed + uvec3(0, 0, depth);

        vec3 target;
        uint mat_type = materials.data[rec.mat_ptr].type;
        vec3 unit_direction = r.direction;

        //metal
        if (mat_type == 1) {
            target = rec.point + reflect(unit_direction, rec.normal);
        // dielectric
        } else if (mat_type == 2) {
            float ref_idx = 1.5;
            float eta = rec.front_face ? (1.0 / ref_idx) : ref_idx;

            float cos_theta = min(dot(-unit_direction, rec.normal), 1.0);
            float sin_theta = sqrt(1.0 - cos_theta*cos_theta);

            if (eta * sin_theta > 1.0 ) { // Too shallow angle -> reflect
                target = rec.point + reflect(unit_direction, rec.normal);
            } else { // Refract
                float reflect_prob = schlick(cos_theta, eta);
                if (hash(seed) < reflect_prob) { // Reflect?
                    target = rec.point + reflect(unit_direction, rec.normal);
                } else {
                    target = rec.point + refract(unit_direction, rec.normal, eta);
                }
            }
        // Diffuse
        } else {
            target = rec.point + random_in_hemisphere(rec.normal, hash2(seed));
        }
        
        r.origin = rec.point;
        r.direction = normalize(target - rec.point);
        
        depth++;

        hitLight = materials.data[rec.mat_ptr].is_light;
        throughput *= materials.data[rec.mat_ptr].albedo;
    }

    if (hitLight) {
        res = materials.data[rec.mat_ptr].albedo * throughput;
    } else {
        res = vec3(0.0);
    }

    return res / float(depth);
}

void main() {
    // Random init stuff
    const ivec2 pixel_coordinates = ivec2(gl_GlobalInvocationID.xy);
    const vec2 image_size = globals.window_size;
    const vec2 uv = vec2(pixel_coordinates) / image_size;
    uvec3 seed = uvec3(pixel_coordinates, globals.num_frames);

    const vec2 sample_pos = (vec2(pixel_coordinates) + rand2(seed)) / image_size;

    if (gl_GlobalInvocationID.x >= image_size.x || gl_GlobalInvocationID.y >= image_size.y) {
        return;
    }

    // Camera
    float theta = radians(globals.vfov);
    float h = tan(theta / 2.0);
    float viewport_height = 2.0 * h;
    float viewport_width = globals.aspect_ratio * viewport_height;

    vec3 vup = vec3(0.0, 1.0, 0.0);

    vec3 w = normalize(globals.look_from - globals.look_at);
    vec3 u = normalize(cross(vup, w));
    vec3 v = cross(w, u);
    vec3 horizontal = globals.focus_dist * viewport_width * u;
    vec3 vertical = globals.focus_dist * viewport_height * v;
    vec3 upper_left_corner = globals.look_from - 0.5*horizontal + 0.5*vertical - globals.focus_dist*w;

    vec2 rd = 0.5 * globals.aperture * random_in_unit_disk(seed);
    vec3 offset = u * rd.x + v * rd.y;

    // Shoot ray
    vec3 ray_dir = normalize(upper_left_corner + sample_pos.x*horizontal - sample_pos.y*vertical - globals.look_from - offset);
    Ray r = Ray(globals.look_from + offset, ray_dir);

    vec3 pixel_color = ray_color(r, seed);

    // Output
    if (globals.num_frames == 0) {
        imageStore(output_image, pixel_coordinates, vec4(pixel_color, 1.0));
    } else {
        vec4 current_color = imageLoad(output_image, pixel_coordinates);
        vec3 new_color = current_color.xyz + pixel_color.xyz;
        imageStore(output_image, pixel_coordinates, vec4(new_color, 1.0));
    }

}
